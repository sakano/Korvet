// Copyright (C) 2015 Biscrat
FILE_INCLUDE_GUARD

Class(Config)
{
    Func(createMode)(mode:string, parent:string = ConfigMode_Default) {
        checkArgs
        typeVoid(this._config[mode])
        if (this._config[mode] !== void) return;
        this._config[mode] = %[ __parent:parent ];
    }

    Func(changeMode)(mode:string) {
        checkArgs
        typeCheck(this._config[mode], "Dictionary")
        if (this._currentMode === mode) return;
        var configFrom = this._config[this._currentMode];
        this._currentMode = mode;

        var configTo = this._getConfigHierarchy(mode);

        // 全ての項目をチェックして値が変わるもののみ適用
        this._config[ConfigMode_Default].getKeys().foreach(LMD(index, name:string) {
            for (var configIndex = 0; configIndex < configTo.count; ++configIndex) {
                if (typeof configTo[configIndex][name] === "undefined") { continue; }
                var valueTo = configTo[configIndex][name];
                if (valueTo != configFrom[name]) {
                    _notifyUpdate(name, valueTo, mode);
                }
            }
        } incontextof %[
            configFrom:configFrom,
            configTo:configTo,
            _notifyUpdate:this._notifyUpdate
        ]);
    }

    Func(newValue)(name:string, value:any+, setCallback:Function) {
        checkArgs
        typeVoid(this._setCallbacks[name])
        typeVoid(this._getCallbacks[name])
        typeVoid(this._config[ConfigMode_Default][name])
        this._setCallbacks[name] = setCallback;
        setValue(name, value, ConfigMode_Default);
    }

    Func(setValue)(name:string, value:any+, mode:string+ = this._currentMode) {
        checkArgs
        this._config[mode][name] = value;
        this._notifyUpdate(name, value, mode);
    }

    Func(getValue)(name:string, mode:string+ = this._currentMode) {
        checkArgs
        var config = this._config[mode];
        while (typeof config[name] === "undefined") {
            typeString(config.__parent)
            config = this._config[config.__parent];
        }
        return config[name];
    }

    Func(serialize)() { notImplemented(); }
    Func(deserialize)(data:Dictionary) { notImplemented(); }

    var _currentMode = ConfigMode_Default;

    var _config = %[
        ConfigMode_Default : %[
            __parent : null
        ]
    ];

    var _setCallbacks = %[];

    Constructor() {
        global.includeObservable(this);
    }

    Finalize() {
        global.finalizeObservable(this);
    }

    Func(_notifyUpdate)(name:string, value:any+, mode:string) {
        this._setCallbacks[name](value);
        if (mode === this._currentMode) {
            triggerEvent(ConfigEventType_ValueUpdate, name, value);
        }
        triggerEvent(ConfigEventType_ValueUpdateAll, name, value, mode);
    }

    Func(_getConfigHierarchy)(mode:string) {
        checkArgs
        var configs = [];
        var config = this._config[mode];
        while (true) {
            configs.add(config);
            if (config.__parent === null) break;
            config = config.__parent;
        }
        assert(configs[configs.count - 1] === this._config[ConfigMode_Default])
        return configs;
    }
}
